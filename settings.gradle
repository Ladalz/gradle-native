rootProject.name = 'gradle-native'

/**
 * Poor man's source dependencies
 * @param repositoryName
 * @param repositoryUrl
 */
void includeExternalBuild(String repositoryName, String repositoryUrl) {
	// Opinionated on my local development layout
	if (file("../${repositoryName}").exists()) {
		includeBuild("../${repositoryName}")

		// Else, just clone the repository
	} else {
		if (!file(".repositories/${repositoryName}").exists()) {
			file('.repositories').mkdirs()
			"git clone ${repositoryUrl} .repositories/${repositoryName}".execute().waitFor()
		} else {
			"git pull".execute(null, file(".repositories/${repositoryName}")).waitFor()
		}
		includeBuild(".repositories/${repositoryName}")
	}
}

includeExternalBuild('toolbox', 'https://github.com/gradle-plugins/toolbox.git')

include 'platformJni'
project(':platformJni').projectDir = file('subprojects/platform-jni')
project(':platformJni').buildFileName = 'platform-jni.gradle'

include 'docs'
project(':docs').projectDir = file('subprojects/docs')
project(':docs').buildFileName = 'docs.gradle'

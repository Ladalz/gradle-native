pluginManagement {
	repositories {
		gradlePluginPortal()
		maven { url = 'https://dl.bintray.com/gradle-plugins/distributions' }
	}
	def gradlePluginVersion = "0.0.56"
	resolutionStrategy {
		eachPlugin {
			if (requested.id.id.startsWith('dev.gradleplugins.')) {
				useModule("${requested.id.id}:${requested.id.id}.gradle.plugin:${gradlePluginVersion}")
			}
		}
	}
}

rootProject.name = 'gradle-native'

includeBuild 'subprojects/templates'
includeBuild 'gradle/plugins/docs-gradle-plugin'

include 'distributions'
project(':distributions').projectDir = file('subprojects/distributions')
project(':distributions').buildFileName = 'distributions.gradle'

include 'languageNative'
project(':languageNative').projectDir = file('subprojects/language-native')
project(':languageNative').buildFileName = 'language-native.gradle'

include 'platformBase'
project(':platformBase').projectDir = file('subprojects/platform-base')
project(':platformBase').buildFileName = 'platform-base.gradle'

include 'platformNative'
project(':platformNative').projectDir = file('subprojects/platform-native')
project(':platformNative').buildFileName = 'platform-native.gradle'

include 'platformIos'
project(':platformIos').projectDir = file('subprojects/platform-ios')
project(':platformIos').buildFileName = 'platform-ios.gradle'

include 'platformJni'
project(':platformJni').projectDir = file('subprojects/platform-jni')
project(':platformJni').buildFileName = 'platform-jni.gradle'

include 'docs'
project(':docs').projectDir = file('subprojects/docs')
project(':docs').buildFileName = 'docs.gradle'

plugins.withId('com.gradle.enterprise') {
	gradleEnterprise {
		buildScan {
			termsOfServiceUrl = "https://gradle.com/terms-of-service"
			termsOfServiceAgree = "yes"

			if (System.getenv('CI')) {
				tag 'CI'
				value('Git Commit SHA', System.getenv('CI_COMMIT_SHA'))
				value('GitLab Runner', System.getenv('CI_RUNNER_DESCRIPTION'))
			} else {
				tag 'LOCAL'
			}

			obfuscation {
				username { name -> 'super-human' }
				hostname { host -> 'somewhere' }
				ipAddresses { addresses -> addresses.collect { '1.2.3.4' } }
			}
		}
	}
}

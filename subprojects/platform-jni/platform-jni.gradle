plugins {
	id 'dev.gradleplugins.java-gradle-plugin'
	id 'maven-publish'
}

repositories {
	jcenter()
	mavenCentral()
	maven { url = 'https://dl.bintray.com/gradle-plugins/distributions' }
}

dependencies {
	api project(':platformBase')
	api project(':platformNative')

	implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"
	implementation "com.google.guava:guava:${guavaVersion}"
	implementation "commons-io:commons-io:${commonsIoVersion}"

	compileOnly "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

tasks.named('javadoc', Javadoc) {
	exclude('**/internal/**')

	StandardJavadocDocletOptions options = it.options as StandardJavadocDocletOptions
	options.setEncoding("utf-8")
	options.setDocEncoding("utf-8")
	options.setCharSet("utf-8")
	options.links("https://docs.oracle.com/javase/8/docs/api", "https://docs.gradle.org/${minimumGradleVersion}/javadoc/")
}

gradlePlugin {
	plugins {
		jniLibrary {
			id = 'dev.nokee.jni-library'
			implementationClass = 'dev.nokee.platform.jni.internal.plugins.JniLibraryPlugin'
		}
	}
	extra {
		minimumGradleVersion = project.minimumGradleVersion
		withJavadocJar()
		withSourcesJar()
	}
}

components.functionalTest {
	dependencies {
		pluginUnderTestMetadata project(':languageNative')

		implementation project(':platformBase')
		implementation project(':platformNative')
		implementation 'dev.nokee:templates:latest.integration'
		implementation "commons-io:commons-io:${commonsIoVersion}"
	}
}
